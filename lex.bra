{lex.bra
ùïåùïãùîΩ-ùü†

analyse bracmat code for lexicality

Usage:
 lex$<symbol>
}

"sOme wEiRdN‚Üíame"=
  new
=   
  . ( lex
    =     Y
          notreffunc
          builtins
          predefs
          CONTXT
          pickVars
          testDeclaration
          A
          AA
          N
          M
          Z
          ZZ
          definedfuncs
          globalfuncs
          globalvars
          jj
          notdeclfuncs
          notdeclvars
          referencedfuncs
          reffunc
          undefinedfuncs
          unrefs
          unusedLocals
          use
      .       ^
            + alc
            + arg
            + asc
            + chr
            + chu
            + clk
            + d2x
            + dbg
            + den
            + div
            + fil
            + flg
            + glf
            + fre
            + get
            + low
            + lst
            + map
            + mem
            + mod
            + mop
            + new
            + pee
            + pok
            + put
            + ren
            + rmv
            + rev
            + sim
            + str
            + swi
            + sys
            + tbl
            + upp
            + utf
            + vap
            + whl
            + x2d
            + "?"
          : ?builtins
        &     cat
            + cos
            + fct
            + flt
            + jsn
            + nestML
            + out
            + sin
            + tay
            + toML
          : ?predefs
        &   0
          : ?notreffunc
          : ?unusedLocals
          : ?notdeclfuncs
          : ?notdeclvars
          : ?referencedfuncs
          : ?undefinedfuncs
          : ?globalvars
          : ?globalfuncs
        & :?definedfuncs
        & ( pickVars
          =   a b
            .     !arg:?a_?b
                & pickVars$!a+pickVars$!b
              | !arg:~
              | 0
          )
        & (CONTXT=.!arg:!sjt ?&?)
        & ( testDeclaration
          =     name
                vars
                unrefs
                A
                a
                b
                Z
                context
                contxt
            .   !arg:(?context.?name.?vars.?unrefs)
              & ( !name:
                |     '$vars
                    : '(? (?.?+#*$name+?) ?)
                  & (     '$unrefs
                        :   
                          ' ( ?A
                              ( CONTXT$($context):?contxt
                              . ?a+#*$name+?b
                              )
                              ?Z
                            )
                      & "Remove function or variable from list of unreferenced functions or variables."
                      &   !A (!contxt.!a+!b) !Z
                        : ?unrefs
                    | 
                    )
                )
              & !unrefs
          )
        & ( variabl
          =   name context vars unrefs
            .   !arg:(?name.?context.?vars.?unrefs)
              & "******** VARIABLE (?var or !var) ********"
              & ( !name:(arg|sjt)
                |   testDeclaration$(!context.!name.!vars.!unrefs)
                  : ?unrefs
                | !globalvars:?+#*!name+?
                |   (     !definedfuncs
                        :   ?aaF
                            (CONTXT$!context.?Af+#*!name+?Zf)
                            ?zzF
                      &   !aaF (!CONTXT.!Af+!Zf) !zzF
                        : ?definedfuncs
                    | 
                    )
                  &   (   !notdeclvars:?A+(!context.?M)+?Z
                        & !A+(!context.!name+!M)+!Z
                      | !notdeclvars+(!context.!name)
                      )
                    : ?notdeclvars
                )
              & !unrefs
          )
        & ( Y
          =     c vars unrefs V
              , name A Z unr context
              , M AA ZZ C N use lhs rhs
              , Af Zf aaF zzF
            .   !arg:(?context.?vars.?unrefs.?arg)
              & (   !arg:(=?lhs_?rhs)
                  & (     !arg
                        : ( 
                          = $(?$?)|$(?'?)
                          )
                      & "******** FUNCTION CALL (function$parameters or function'parameters) ********"
                      & (   !lhs:
                          & ( !rhs:@:~&
                            | 
                            )
                        | !builtins+!predefs:?+!lhs+?
                        | !lhs:(=?.?)
                        |   !('(flg'$lhs))
                          : (=$!.?)
                        |   (     testDeclaration
                                $ (!context.!lhs.!vars.!unrefs)
                              : ?unrefs
                            | !globalfuncs:?+#*!lhs+?
                            |   (     !notdeclfuncs
                                    : ?A+(!context.?M)+?Z
                                  & !A+(!context.!lhs+!M)+!Z
                                | !notdeclfuncs+(!context.!lhs)
                                )
                              : ?notdeclfuncs
                            )
                          & (     !definedfuncs
                                :   ?A
                                    ( CONTXT$!context:?C
                                    . ?AA+#?N*(!lhs.?use)+?ZZ
                                    )
                                    ?Z
                              &     !A
                                    ( !C
                                    .   !AA
                                      + !N*(!lhs.!use+Referenced)
                                      + !ZZ
                                    )
                                    !Z
                                : ?definedfuncs
                            |   (     !undefinedfuncs
                                    : ?A+(!context.?M)+?Z
                                  & !A+(!context.!lhs+!M)+!Z
                                | !undefinedfuncs+(!context.!lhs)
                                )
                              : ?undefinedfuncs
                            )
                        )
                      &     Y
                          $ (!context.!vars.!unrefs.'$rhs)
                        : ?unrefs
                    |   !arg:(=$(?=?))
                      &   (!lhs:~|^)
                        : ?lhs
                      & (   '$rhs:(=?V.?c)
                          & "******** FUNCTION DEFINITION (function=local vars.definition) ********"
                          & pickVars$!V:?V
                          & ( !lhs:^
                            |   (     testDeclaration
                                    $ (!context.!lhs.!vars.!unrefs)
                                  : ?unrefs
                                |   !context:
                                  & !globalfuncs+!lhs:?globalfuncs
                                |   (     !notdeclfuncs
                                        : ?A+(!context.?M)+?Z
                                      & !A+(!context.!lhs+!M)+!Z
                                    | !notdeclfuncs+(!context.!lhs)
                                    )
                                  : ?notdeclfuncs
                                )
                              &   (     !definedfuncs
                                      : ?A (!context.?M) ?Z
                                    & (     !M
                                          : ?+#*(!lhs.?)+?
                                        & !definedfuncs
                                      |   !A
                                          (!context.(!lhs.0)+!M)
                                          !Z
                                      )
                                  |   (!context.!lhs.0)
                                      !definedfuncs
                                  )
                                : ?definedfuncs
                            )
                          &     Y
                              $ ( !context !lhs
                                .   (!context !lhs.!V)
                                    !vars
                                .   (!context !lhs.!V)
                                    !unrefs
                                . '$c
                                )
                            : ?unrefs
                          & (     !unrefs
                                :   ?
                                    ( !context !lhs
                                    . %+?:?unr
                                    )
                                    ?
                              &     (!context !lhs.!unr)
                                  + !unusedLocals
                                : ?unusedLocals
                            | 
                            )
                        |   "******** VARIABLE DEFINITION (variable=value) ********"
                          & ( !lhs:^
                            |     testDeclaration
                                $ (!context.!lhs.!vars.!unrefs)
                              : ?unrefs
                            |   !context:
                              & !globalvars+!lhs:?globalvars
                            |   (     !notdeclvars
                                    : ?A+(!context.?M)+?Z
                                  & !A+(!context.!lhs+!M)+!Z
                                | !notdeclvars+(!context.!lhs)
                                )
                              : ?notdeclvars
                            )
                          &     Y
                              $ (!context.!vars.!unrefs.'$rhs)
                            : ?unrefs
                        )
                    |       Y
                          $ (!context.!vars.!unrefs.'$lhs)
                        : ?unrefs
                      &     Y
                          $ (!context.!vars.!unrefs.'$rhs)
                        : ?unrefs
                    )
                |     !arg
                    : (=$!|$?)
                  &   flg$!arg
                    : (=?.%?name (|))
                  &   variabl$(!name.!context.vars.!unrefs)
                    : ?unrefs
                | 
                )
              & !unrefs
          )
        & (   '$lex:(=? !arg ?.?)
            & out$(!arg is local var in lex)
            &     Y
                $ (...'($arg=$(-2$!arg)))
              : ?unrefs
            & -1$!arg
          |   Y$(...'($arg=$!arg))
            : ?unrefs
          )
        &   put
          $ ( "==========================================================
Lexical analysis. See program code further down this file.
==========================================================

 ----------------------
{ Undeclared functions }
 ----------------------
"
            , "lex.txt"
            , NEW BIN
            )
        & lst$(notdeclfuncs,"lex.txt",APP BIN)
        &   put
          $ ( "
 ----------------------
{ Undeclared variables }
 ----------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(notdeclvars,"lex.txt",APP BIN)
        & !definedfuncs:?reffunc
        & 0:?notreffunc
        &   whl
          ' (   !reffunc
              : ?A (?M.?AA+#?N*(?jj.0)+?ZZ) ?Z
            & !notreffunc+(!M !jj):?notreffunc
            & !A (!M.!AA+!ZZ) !Z:?reffunc
            )
        &   put
          $ ( "
 ------------------------
{ Unused local variables }
 ------------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(unusedLocals,"lex.txt",APP BIN)
        &   put
          $ ( "
 ----------------
{ Not referenced }
 ----------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(unrefs,"lex.txt",APP BIN)
        &   put
          $ ( "
 -------------------
{ Defined functions }
 -------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(definedfuncs,"lex.txt",APP BIN)
        &   put
          $ ( "
 ---------------------
{ Undefined functions }
 ---------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(undefinedfuncs,"lex.txt",APP BIN)
        &   put
          $ ( "
 ------------------------
{ Unreferenced functions }
 ------------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(notreffunc,"lex.txt",APP BIN)
        &   put
          $ ( "
 ----------------------
{ Referenced functions }
 ----------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(reffunc,"lex.txt",APP BIN)
        &   put
          $ ( "
 ------------------
{ Global variables }
 ------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(globalvars,"lex.txt",APP BIN)
        &   put
          $ ( "
 ------------------
{ Global functions }
 ------------------
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(globalfuncs,"lex.txt",APP BIN)
        &   put
          $ ( "

============
Program code
============
"
            , "lex.txt"
            , APP BIN
            )
        & lst$(!arg,"lex.txt",APP BIN)
        & "
===========
See lex.txt
==========="
    );

rr=
  get'"lex.bra"
& rmv$(str$(lex ".bak"))
& ren$("lex.bra".str$(lex ".bak"))
&   put
  $ ( "{lex.bra
ùïåùïãùîΩ-ùü†

analyse bracmat code for lexicality

Usage:
 lex$<symbol>
}

"
    , "lex.bra"
    , NEW BIN
    )
& lst'("sOme wEiRdN‚Üíame","lex.bra",APP BIN)
& put'(\n,"lex.bra",APP BIN)
& lst'(rr,"lex.bra",APP BIN)
& put$("
new'\"sOme wEiRdN‚Üíame\";
","lex.bra",APP BIN);

new'"sOme wEiRdN‚Üíame";
